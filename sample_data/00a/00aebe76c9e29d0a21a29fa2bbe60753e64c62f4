r[t_] := Range[-t, t]

possibleValues = Flatten[Outer[List, r@1, r@2, r@4, r@8, r@16, r@32], 5];

mc[i_, j_, m_][L_] := Mod[L[[i]] - L[[j]], m] == 0

consistentValues = Select[possibleValues,
  mc[1, 3, 2][#] && mc[1, 4, 3][#] && mc[1, 5, 4][#] && mc[1, 6, 5][#]
     && mc[2, 4, 2][#] && mc[2, 5, 3][#] && mc[2, 6, 4][#] && mc[3, 6, 3][#] &];

consistentPolynomials5 = Expand@InterpolatingPolynomial[Transpose[{Range[0, 5], #}], x]&
  /@ consistentValues;

centerPolynomialAt[p_, t_] := p - Product[x-j, {j, 0, t-1}] (Round[p /. x->t, t!/2])/(t!)

possiblePolynomials6 = centerPolynomialAt[#, 6] & /@ consistentPolynomials5;

consistentPolynomials6 = Select[possiblePolynomials6, Abs[(# /. x -> 6)] <= 2^6 &];

possiblePolynomials7 = centerPolynomialAt[#, 7] & /@ consistentPolynomials6;

consistentPolynomials7 = Select[possiblePolynomials7, Abs[(# /. x -> 7)] <= 2^7 &];

possiblePolynomials8 = centerPolynomialAt[#, 8] & /@ consistentPolynomials7;

consistentPolynomials8 = Select[possiblePolynomials8, Abs[(# /. x -> 8)] <= 2^8 &];

consistentPolynomials8positive = Select[consistentPolynomials8,
  Last@CoefficientList[#, x] > 0 &];

SortBy[consistentPolynomials8positive, Reverse@CoefficientList[#, x] &]